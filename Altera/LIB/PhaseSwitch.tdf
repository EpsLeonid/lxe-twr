TITLE "Phase_Switch";

-- This file includes functions for automatic switching of refrence clock
-- sourses - internal quartz 25MHz or exturnal Phase0 signal. 
-- constant FmaxThresh = 26000 ; -- Upper limit in kHz
-- constant FminThresh = 24000 ; -- Lower limit in kHz
-- constant RefClock   = 100000 ; -- PLL'd Local Quartz Freq.

INCLUDE "lpm_counter.inc" ;

PARAMETERS
(
FmaxThresh  = 26000 ,   -- Upper limit in kHz
FminThresh  = 24000 ,   -- Lower limit in kHz
RefClock    = 25000     -- PLL'd Local Quartz Freq.
);

SUBDESIGN  PhaseSwitch
(
clock           : input;  -- Global FPGA clock 100MHz
LinkClk         : input;  -- Input from Pin160 "Phase0"
Reset           : input;  -- Global FPGA Reset, active High
	
Phase25         : output; -- Output to Pin144  
LinkClk_Selected: output; -- Signal indicates what Clk source is selected
)

VARIABLE 

--	ClockDivider[1..0]  : DFF;
TimingCnt  : lpm_counter WITH ( -- Cycle Timing counter
    LPM_WIDTH = 16,
	LPM_TYPE = "LPM_COUNTER",
	LPM_DIRECTION = "Up");
Phase0Cnt  : lpm_counter WITH ( -- Phase freq.  counter
	LPM_WIDTH = 16,
	LPM_TYPE = "LPM_COUNTER",
	LPM_DIRECTION = "Up");
	Sync_TRG1 : DFF ;              -- Synchronize comparator	
	Sync_TRG2 : DFF ;              -- Synchronize comparator	

Cycle_End   : node ;
--Test1       : node ;

BEGIN

--============================================================================
-- Frequency diskriminator section begin 
		-- Base time interval
IF (TimingCnt.q[15..0] == RefClock) -- Base time interval counter 25000 == 1ms
    Then  Sync_TRG1.d=VCC;
    Else  Sync_TRG1.d=GND;
End IF;

Cycle_End = Sync_TRG1.q    ;
TimingCnt.sclr  = Cycle_End OR Reset ;
Phase0Cnt.sclr  = Cycle_End OR Reset ;
Sync_TRG2.clk   = Cycle_End  ;
LinkClk_Selected=Sync_TRG2.q    ;

TimingCnt.clock = clock ;
Phase0Cnt.clock = LinkClk ;
Sync_TRG1.clk   = clock ;

Sync_TRG1.clrn  = !Reset;
Sync_TRG2.clrn  = !Reset;
		-- Pulse counter & CMP
if ( (Phase0Cnt.q[15..0] > FmaxThresh) OR (Phase0Cnt.q[15..0] < FminThresh))
    Then  Sync_TRG2.d=GND;
    Else  Sync_TRG2.d=VCC;
end if;

--============================================================================
-- Mux section begin 
Phase25 = ((LinkClk and LinkClk_Selected) OR (clock and !LinkClk_Selected));

END;